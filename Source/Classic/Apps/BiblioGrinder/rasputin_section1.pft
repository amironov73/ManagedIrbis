mhl,
'\fi360',
if s(v982^s,v982^k)<>'' then v903' '&unifor("kstr.mnu|"v982^s),", "d982^k,&unifor('Av982^k#1')," "d982^i,&unifor('Av982^i#1'),". "d982^k,'\par\keep ',fi,

if p(v461)then 
   '\b '&unifor('E3'v461^c),'\b0  ',
   if &unifor('F3'v461^c)<>''then' '&unifor('F3'v461^c) fi
   if s(v230v135)<>''then ' [Электронный ресурс]' fi,
   | = |v46^l,| : |v461^e,| / |v461^f,
   |. - |v461^p,(". - "| ; |+v461^d,| : |v461^g),
   |. - |v461^h,(|. - (|v46^a,| ; |v46^v,
   if s(v46^a,v46^v)<>''then|)|d46 fi,   |. - |v46^p),
   if s(v982^1,v982^4,v982^5)<>''then'. - 'v982^1|; |,|№ ГР |v982^4,|; Инв.№ |v982^5 fi,
fi,
"\par\pard\keep\b "d461,
"\b "v200^v"\b0 ",
if p(v200^v) and p(v200^a) then' : 'fi,

if p(v461) then v200^a else 
   '\b '&unifor('E3'v200^a),'\b0 ',
      if &unifor('F3'v200^a)<>'' then' '&unifor('F3'v200^a) fi 
fi,
if a(v461) then if s(v230v135)<>''then ' [Электронный ресурс]' fi fi,
if v920:'NJ'then'\b '
   ref(l(|I=|v933),&unifor('E3'v200^a)'\b0 ',
      if &unifor('F3'v200^a)<>''then' '&unifor('F3'v200^a) fi," [Электронный ресурс]"d230,| / |v200^f,
      ". - "d230^a,(if p(v230^a) then ""| и |+v230^a fi),". "d110^d,
      if &unifor('Krksp.mnu|'v110^d)<>''then &unifor('Krksp.mnu|'v110^d) else"Периодичность "v110^d fi),
   '\b '," - "v934"г.",
   if p(v935) then ref(l("I="v933),if p(v210^8) then' 'v210^8' 'else' т.'fi),v935 fi,
   if p(v936) then ref(l("I="v933),if p(v210^9) then' 'v210^9' 'else' N 'fi),v936 fi,
   '\b0 ',
   if p(v922) then'\par ',if &unifor('Av905^k#1')<>''then'Из содержания: 'else'Содержание: 'fi fi,
   (if p(v922) then v922^c,| / |v922^f,|,|v922^2,|,|v922^3,
       if p(v922^f)then'. 'else v922.1+| ; |,
    fi fi)
else 
   if p(v922) then &uf('6ibis922'), 
      (| = |v510^d,| : |v510^e,| / |v510^f,| ; |v510^g),| : |v924 
   else
      if s(v510^e)<>''then| : |v200^e,(|. |v923^h,| : |v923^i,|. |v923^k,| : |v923^l) fi,
      if s(v510^f)<>''then| / |v200^f,| ; |v200^g fi,
      (| = |v510^d,| : |v510^e,| / |v510^f,| ; |v510^g),| : |v924,
      if s(v510^e)=''then| : |v200^e,(|. |v923^h,| : |v923^i,|. |v923^k,| : |v923^l) fi,
      if s(v510^f)=''then | / |v200^f,| ; |v200^g,
fi fi fi,
". - "v205^a,if p(v205^a)then" / "v205^f else". - "v205^f fi,
if a(v922)then (if v123^x:'0'then ". - 1 : "|, 1 : |+v123^d,| ; 1 : |v123^e,v123^f fi) fi,
|. - № |v982^z,| ; Заявл. |v982^r*6|.|,v982^r*4.2|.|,v982^r.4,| ; Опубл. |v982^p*6|.|,
v982^p*4.2|.|,v982^p.4,|, |v982^m,| ; Приор. |v982^v*6|.|,v982^v*4.2|.|,v982^v.4,|, № |v982^x,
if p(v982^y) then' ('&unifor('Kstr.mnu|'v982^y)')'fi,
if &unifor('Av982^k#2')<>'' then' ; '&unifor('Av982^k#2')' '&unifor('Av982^i#2') fi,
". - Взамен "v982^a,
if s(v982^b,v982^c)<>''then if a(v982^a)then'. - 'else' ; 'fi fi,"Введ. с "v982^b," по "v982^c,
if p(v328^c)then". - Защищена "v328^c*6'.'v328^c*4.2'.'v328^c.4 fi,
if p(v328^d)then " ; "d328^c,". -"n328^c,"Утв. "v328^d*6'.'v328^d*4.2'.'v328^d.4 fi, " ; "v328^e,
if a(v461) then". - "d230^a,(if p(v230^a) then ""| и |+v230^a fi) fi,
if s(v210^a)=''then 
   if s(v461^d,v463v110v981v982^z)<>''then else 
      if &unifor('Kjzk.mnu|'&unifor('Av101#1'))<>''then'. - [б. м.]'else'. - [s. l.]'fi 
fi fi, 
if &unifor('Av210^d#2')<>'' then 
   (if p(v210) then ". - "| ; |+v210^a,| ; |v210^x,| ; |v210^y,| : |v210^c, 
       if &unifor('Av461#1')<>''then 
          if s(v210^a,v210^c)<>''then|, |v210^d else|. - |v210^d fi 
       else |, |v210^d 
    fi fi),
else (". - "| ; |+v210^a,| ; |v210^x,| ; |v210^y,| : |v210^c), 
   if p(v230) and s(v210^a,v210^c,v461)='' then  
      if &unifor('Kjzk.mnu|'&unifor('Av101#1'))<>''then' : [б. и.]'else' : [s. n.]'fi 
   fi,
   if p(v461) then 
      if s(v210^a,v210^c)<>''then|, |v210^d else|. - |v210^d fi 
   else |, |v210^d 
fi fi,
if s(v933,v461,v463,v110,v981v982^z)<>''then else 
   if s(v210^d)='' then 
      if &unifor('Kjzk.mnu|'&unifor('Av101#1'))<>''then', [б. г.]'else', [s. a.]'fi 
fi fi,
if v920='J'then" - "d210^d" - ",v210^e,"    "n210^e fi," ("v210^1")",
(". - "| ; |+v215^a,| |d215^1,&unifor('G2='v215^1),
if a(v215^1)then 
   if v215^a:'с'or v215^a:'c'or v215^a:'p'or v215^a:'S'then else 
      if p(v215^a) then &unifor('Korg.mnu|4') fi,
      if s(v215^c,v215^d,v215^e)<>''then |.|d215^a 
fi fi fi,| : |d215^c,
if v215^c*1.1='-'then v215^c*2,|, |v215^0*2,|, |v215^7*2,|, |v215^8*2 else v215^c fi,
if s(&unifor('G2='v230^b),&unifor('G2='v230^c))<>''then' : '&unifor('G2='v230^b),
   if &unifor('G2='v230^c)<>''then if &unifor('G2='v230^b)<>''then', 'fi,&unifor('G2='v230^c) 
fi fi),
(if v123^x:'1' then| : \b |v123^a*3|\b0 |,|, |v123^b*3 fi),
(if p(v225)then 
    if v225^n:'0'then else|. - (|v225^a,| = |v225^d,| : |v225^e,| / |v225^f,|, ISSN |v225^x,| ; |v225^v,|. |v225^h,
       if p(v225^h) then|, |v225^i else |. |v225^i fi,| = |v225^1,| ; |v225^w,|. |v225^k,
       if p(v225^k) then|, |v225^l else |. |v225^l fi,| = |v225^2,| ; |v225^y,|)|d225 
 fi fi),
(if v517^n:'0'then else|. - |v517^t*4,| : |v517^a fi),
". - \b Пер.изд.: \b0"v454^a," / "v454^d,", "v454^e,", "v454^f,". - "v454^p,
". - "v454^g,", "v454^h,". - "v963^1,
if v920:'NJ' then else if p(v463) then ,&uf('6OISTASPK'), fi fi,
|. - |v912,|. - |v314,
if s(v461^j)=''then". - \b ISSN \b0 "|. - |+v11^a fi,

if p(v101) and v101 <> &uf('Korg.mnu!6') then 
  '. - ',
  if &uf('+N101') = '1' then 'Язык' else 'Языки' fi,
  ': ',  
  (
    if p(v101) then 
      if &uf('+90')<>'1' then 
        if &uf('+90') = &uf('+N101') then ' и ' else ', ' fi,
      fi
      &uf('Q',&uf('Kjz.mnu!',v101)), 
    fi
   )
   '.'
fi,

if v900:'l' and p(v951^i) then '. - Режим доступа: ', v951^i, ', свободный. - Загл. с экрана. ', fi,

if not v920:'ASP' then |. - |v327, fi
 
|\par\pard\fi360 |v331,

if v920.1:'A' then else if s(v905^m)=''and &unifor('IPRIVATE,FPP,')=''then mhl,"\par  "|. |+v300,mpl fi fi, 
"\par \b   Перевод заглавия: \b0  "v541, 
,&uf('6oant'), 
if p(v391)then" \par \b Есть автограф: \b0 "d391, 
   (if p(v391)then |Экз. |v391^!| : |,v391^a,|, |v391^g, 
       if a(v391^g) then |, |d391^b 
          if v391^b:'. 'then v391^b else 
             if v391^b:'.'then &unifor('G0.'v391^b)'. '&unifor('G2.'v391^b) else v391^b 
       fi fi fi,| (|v391^c|)|,|  |v391^t,mhl,v391.1+|; |,mpl fi) 
fi, 
("\par \b Экслибрис: \b0 "| ; |+d398,if p(v398^!)then |\par \b Экз.|v398^!|: \b0 |,v398^a else if s(v398^a,v398^b)=''then v398 else v398^b," ("v398^a")" fi fi,) 
"\par \b Пометки автора: \b0 "| ; |+v395, 
"\par \b Пометки владельца коллекции: \b0 "| ; |+v396, 
("\par \b Сохранность документа: \b0 "| ; |+d399, if s(v399^!,v399^a)='' then v399 else|\par \b Экз.|v399^!|: \b0 |,v399^a fi)
,&uf('6opri451'), 
,&uf('6opril421'), 
,&uf('6Opris488'),
,&uf('6opri699'),

if val(&unifor('J,I470='v903))>0 then '\par ' &unifor('+I?2,,,,I470='v903'?Есть рецензия / реферат') fi, 
if p(v330)then 
   if a(v905^k) then'\par \tab {\b Содержание:}', 
      (if p(v330)then'\par \b '
          if a(v330^f) then v330^8|. | fi,
          if p(v330^q) then else 
            if p(v330^x) then 
            &uf('+S1'
               if v330^x='1' then v330^f 
               else &unifor('E'v330^x,v330^f), 
                  if p(v330^?)then |, |v330^? else 
                     if &unifor('F'v330^x,v330^f)<>'' then ', '&unifor('F'v330^x,v330^f) fi fi,'.' 
               fi) 
            else 
            &uf('+S1'
               if v330^f:',' then v330^f|.| else 
                  if v330^f:' ' then &unifor('G0 'v330^f),|, |v330^?|.| 
                     if a(v330^?) then 
                        if v330^f:'.' then ', ' 
                           if v330^f:'. ' 
                           then &unifor('G2 'v330^f) else &unifor('G0.'&unifor('G2 'v330^f)),'. ' &unifor('G2.'v330^f) 
                           fi 
                        else ', '&unifor('G2 'v330^f)'.' 
                     fi fi 
                  else v330^f,|, |v330^?,|.|d330^f, 
                  fi 
               fi) 
            fi,| |d330^f, 
         fi
         if s(v330^(,v330^))<>''then v330^(,| |v330^),'. 'fi,
         if p(v330^u) then if f(val(v330^u),0,0)=v330^u then else if v330^u:'/' then &uf('G2/'v330^u)'. ' fi fi fi, 
         if v330^]<>''then if v330^[='0'then &unifor('+I$3,10,,'v330^]'$',v330^c)else,if v330^[='1'then &unifor('+I$0,,'v330^]'$',v330^c)else &uf('+S1'v330^c)fi fi else &uf('+S1'v330^c)fi'\b0 ',
         | = |d330^d,&uf('G0('v330^d),,| : |v330^e, 
         if a(v330^c) then else 
            if s(v330^f,v330^g)<>'' and &unifor('Av905^1#1')='' then 
               if (p(v330^g) and (not(v330^g:'<')) and (not(v330^g|!|:'>!'))) or 
                  a(v330^g) and p(v330^f) then' / 'fi ,o330g, fi fi,
         if p(v330^4)then'. -\b ', 
            if v330^4:'s'or v330^4:'p'or v330^4:'с'then 
            else mpu,
               if &uf('Kjz_str_ogl.mnu\'&uf('Av101#1'))<>'' then &uf('Kjz_str_ogl.mnu\'&uf('Av101#1')) 
               else &unifor('Korg.mnu|4') fi,mpl,'. \b0 ' 
            fi,v330^4, 
         fi,
         if v330^0*1.1='-'then| : |v330^0*2 else| : |v330^0 fi, |. - |v330^p, 
         if p(v330^w) then'\par Другие авторы: 'v330^2,', 'v330^3,', 'v330^w fi, 
         if s(&unifor('Av905^f#1'),&unifor('IPRIVATE,FPK,'))='1'then 
         else|\par Кл.слова: |d330^5,mhl,
            if s(&unifor('Av905^f#1'),&unifor('IPRIVATE,FPK,'))='2' 
            then v330^5,|, |v330^6,|, |v330^7 
            else 
               if s(&unifor('Av905^f#1'),&unifor('IPRIVATE,FPK,'))='3' 
               then &unifor('Q'v330^5,|, |v330^6,|, |v330^7), 
               else v330^5,|, |v330^6,|, |v330^7 
               fi mpl,
            fi fi 
      fi)
fi fi, 

(if p(v300) then
"\par \b   Общие примечания: \b0  "v300, /,
fi,)

(if p(v314) then
"\par \b   Примечания об интеллектуальной ответственности: \b0  "v314, /,
fi,)

'\par\pard ',
mpl, &uf('!'),
